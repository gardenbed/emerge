grammar ebnf

PREDEF = /\$[A-Z][0-9A-Z_]*/
IDENT  = /[a-z][0-9a-z_]*/
TOKEN  = /[A-Z][0-9A-Z_]*/
STRING = /"([^\\"]|\\[\\"'tnr]|\\x[0-9A-Fa-f]{2}|\\u[0-9A-Fa-f]{4}|\\U[0-9A-Fa-f]{8})*"/
REGEX  = /\/([^\/\\*]|\\.)([^\/\\]|\\.)*\//

@left  <rhs = rhs rhs>
@left  "(" "[" "{" "{{" IDENT TOKEN STRING
@right "|"
@none  "="
@none  "@left" "@right" "@none"

start     = name {decl};
name      = "grammar" IDENT [";"];
decl      = token [";"] | directive [";"] | rule ";";
token     = TOKEN "=" (STRING | REGEX | PREDEF);
directive = ("@left" | "@right" | "@none") {{term | "<" rule ">"}};
rule      = lhs "=" [rhs];
lhs       = nonterm;
rhs       = rhs rhs | "(" rhs ")" | "[" rhs "]" | "{" rhs "}" | "{{" rhs "}}" | rhs "|" rhs | rhs "|" | nonterm | term;
nonterm   = IDENT;
term      = TOKEN | STRING;
